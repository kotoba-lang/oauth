(ns oauth.adapters.client-auth
  (:require [kotoba.lang.text :as str]
            [oauth.adapters.http :as http]))

(defn- basic-token [client-id client-secret]
  #?(:clj (let [raw (str client-id ":" client-secret)]
            (.encodeToString (java.util.Base64/getEncoder)
                             (.getBytes raw "UTF-8")))
     :cljs (js/btoa (str client-id ":" client-secret))))

(defn apply-client-auth [opts form-or-body]
  (let [auth (:client-auth opts)
        method (:method auth)]
    (case method
      :client-secret-basic
      [(assoc-in opts [:headers "Authorization"]
                 (str "Basic " (basic-token (:client-id auth) (:client-secret auth))))
       form-or-body]

      :client-secret-post
      [opts (merge form-or-body
                   {:client_id (:client-id auth)
                    :client_secret (:client-secret auth)})]

      :bearer
      [(assoc-in opts [:headers "Authorization"] (str "Bearer " (:token auth)))
       form-or-body]

      :private-key-jwt
      [opts (merge form-or-body
                   {:client_id (:client-id auth)
                    :client_assertion_type "urn:ietf:params:oauth:client-assertion-type:jwt-bearer"
                    :client_assertion (:client-assertion auth)})]

      [opts form-or-body])))

(defn auth-client [client]
  (reify http/IHttpClient
    (get-json! [_ url opts]
      (http/get-json! client url opts))
    (post-form! [_ url form opts]
      (let [[opts form] (apply-client-auth opts form)]
        (http/post-form! client url form opts)))
    (post-json! [_ url body opts]
      (let [[opts body] (apply-client-auth opts body)]
        (http/post-json! client url body opts)))))

(defn redact-client-auth [opts]
  (if-let [auth (:client-auth opts)]
    (assoc opts :client-auth
           (-> auth
               (dissoc :client-secret :token :client-assertion)
               (assoc :redacted? true)))
    opts))
