(ns oauth.adapters.retry
  (:require [oauth.adapters.http :as http]))

(defn- retryable? [result]
  (or (= :http/status (:error result))
      (= :transport (:error result))
      (and (:status result) (>= (:status result) 500))))

(defn backoff-delay-ms [attempt opts]
  (let [base (or (:base-delay-ms opts) 100)
        max-delay (or (:max-delay-ms opts) 5000)]
    (min max-delay (* base (long #?(:clj (Math/pow 2 (dec attempt))
                                    :cljs (.pow js/Math 2 (dec attempt))))))))

(defn- call-with-retry [f attempts opts]
  (loop [n 1
         last-result nil]
    (let [result (f)]
      (if (and (< n attempts) (retryable? result))
        (do
          (when-let [sleep! (:sleep! opts)]
            (sleep! (backoff-delay-ms n opts) {:attempt n :result result}))
          (recur (inc n) result))
        (or result last-result)))))

(defn retry-client
  ([client] (retry-client client {}))
  ([client opts]
   (let [attempts (max 1 (or (:attempts opts) 3))]
     (reify http/IHttpClient
       (get-json! [_ url call-opts]
         (call-with-retry #(http/get-json! client url call-opts) attempts opts))
       (post-form! [_ url form call-opts]
         (call-with-retry #(http/post-form! client url form call-opts) attempts opts))
       (post-json! [_ url body call-opts]
         (call-with-retry #(http/post-json! client url body call-opts) attempts opts))))))
