(ns oauth.adapters.discovery-test
  (:require [clojure.test :refer [deftest is]]
            [oauth.adapters.discovery :as discovery]
            [oauth.adapters.http :as http]))

(deftest discovers-provider-metadata-through-http-client
  (let [calls (atom [])
        client (reify http/IHttpClient
                 (get-json! [_ url opts]
                   (swap! calls conj [url opts])
                   {:authorization_endpoint "https://idp.example/authorize"
                    :token_endpoint "https://idp.example/token"
                    :introspection_endpoint "https://idp.example/introspect"
                    :jwks_uri "https://idp.example/jwks"
                    :scopes_supported ["openid" "profile"]
                    :grant_types_supported ["authorization_code" "refresh_token"]})
                 (post-form! [_ _ _ _] (throw (ex-info "unexpected" {})))
                 (post-json! [_ _ _ _] (throw (ex-info "unexpected" {}))))
        out (discovery/discover client "https://idp.example/" {:fetched-at "2026-07-01T00:00:00Z"})]
    (is (= "https://idp.example/token" (:oauth.provider/token-endpoint out)))
    (is (= #{"openid" "profile"} (:oauth.provider/scopes-supported out)))
    (is (= [["https://idp.example/.well-known/oauth-authorization-server"
             {:fetched-at "2026-07-01T00:00:00Z"}]]
           @calls))))

(deftest caches-provider-metadata-until-refresh
  (let [calls (atom 0)
        client (reify http/IHttpClient
                 (get-json! [_ _ _]
                   (swap! calls inc)
                   {:token_endpoint (str "https://idp.example/token-" @calls)})
                 (post-form! [_ _ _ _] nil)
                 (post-json! [_ _ _ _] nil))
        discover! (discovery/cached-discoverer client)]
    (is (= "https://idp.example/token-1"
           (:oauth.provider/token-endpoint (discover! "https://idp.example" {}))))
    (is (= "https://idp.example/token-1"
           (:oauth.provider/token-endpoint (discover! "https://idp.example" {}))))
    (is (= "https://idp.example/token-2"
           (:oauth.provider/token-endpoint (discover! "https://idp.example" {:refresh? true}))))
    (is (= 2 @calls))))
